//
// Initialization of the PC HAL.
//

#include "<df>/dragonfruit.h"
#include "../../OSLoader/OSLoaderGlobal.h"

#include "<inc>/HALDriver.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALCrash.h"
#include "<inc>/HALLog.h"

#include "<ll>/OSDLL/OS.h"

var InfoTotalRAM 0

var KeThreadCurrentStackTop 0
public KeThreadCurrentStackTop

var HALPlatformKernelPageDirectory 0
public HALPlatformKernelPageDirectory

buffer HALPlatformModel 32
public HALPlatformModel

buffer HALCPUModel 64
public HALCPUModel

table KiI386Gdt
    0x00000000 0x00000000 // Null (used as GDTR)
    0x0000ffff 0x00cf9b00 // Kernel code
    0x0000ffff 0x00cf9300 // Kernel data
    0x0000ffff 0x00cffb00 // User code
    0x0000ffff 0x00cff300 // User data
    0x0000ffff 0x00cff300 // User TEB
    0x00000000 0x00000000 // Task
endtable
public KiI386Gdt

fn HALPlatformInfo { -- }
	// we're supposed to print platform info in this function

    HALCPUModel
	InfoTotalRAM@ 1048576 /
	"PC (%dMB) i386 %s" Printf
end

extern HALPCGetCPUModel { -- modeldata }

extern HALPCPICInit { -- }

fn HALPlatformInit { ldrinfo -- ret }
	fnsection "INIT$text"

    0 ret!

	HALPlatformModel
	"PC"
	strcpy

    auto modeldata
    HALPCGetCPUModel modeldata!

    if (modeldata@)
        if (modeldata@ -1 ~=)
            auto family
            auto model
            auto stepping
            modeldata@ 8 >> 0xf & family!
            modeldata@ 4 >> 0xf & model!
            modeldata@ 0xf & stepping!

            if (family@ 0xf == family@ 0x6 == ||)
                modeldata@ 12 >> 0xf0 & model |=
            end

            if (family@ 0xf ==)
                modeldata@ 20 >> 0xff & family +=
            end

            HALCPUModel dup strlen + " Family " strcpy
            family@ HALCPUModel dup strlen + itoa
            HALCPUModel dup strlen + ", Model " strcpy
            model@ HALCPUModel dup strlen + itoa
            HALCPUModel dup strlen + ", Stepping " strcpy
            stepping@ HALCPUModel dup strlen + itoa
        end elseif (HALCPUModel gb ~~)
            HALCPUModel "Generic i386" strcpy
        end
    end

    ldrinfo@ LdrInfo_TotalRAM + @ InfoTotalRAM!

    HALPCPICInit

    "Platform: " "HALMain" HALLog
    HALPlatformInfo
end

fn HALPlatformCrash { -- }
end

extern HALCPUHalt { -- }

extern HALI386Reset { -- }

fn HALPlatformExit { ret mode -- }
	// MUST BE CALLED WITH INTERRUPTS DISABLED

	if (mode@ OSSHUTDOWN_HALT ==)
        //while (1)
        //    HALCPUHalt
        //end
	end elseif (mode@ OSSHUTDOWN_REBOOT ==)
		HALI386Reset
	end else
		"?" Printf

		HALI386Reset
	end
end

fn HALPlatformIsComputerOnFire { -- isonfire ok }
    // we don't actually *know* the computer isn't on fire, but this seems like a decent guess

    0 isonfire!
    0 ok!
end

fn HALNvramQuery { query -- ok }
    STATUS_NOT_SUPPORTED ok!
end

fn HALNvramSet { delete query -- ok }
    STATUS_NOT_SUPPORTED ok!
end

fn HALNvramRead { query index -- nextindex ok }
    STATUS_NOT_SUPPORTED ok!
end
