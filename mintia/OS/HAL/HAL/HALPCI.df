//
// Implements the parts of PCI support that need to be in the HAL.
//

#include "<df>/dragonfruit.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALPCI.h"

#include "<inc>/HALIPL.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"
#include "<inc>/HALCrash.h"

#include "<ll>/OSDLL/OS.h"

var HALPCIEnumerateFunction 0
public HALPCIEnumerateFunction

fn HALPCIEnumerate { func vendor devid revision class interface -- count }
	auto enumfunc
	HALPCIEnumerateFunction@ enumfunc!

	if (enumfunc@ ~~)
        0 count!
        return
	end

	func@ vendor@ devid@ revision@ class@ interface@
	enumfunc@ HALPCIEnumerateF count!
end

fn HALPCIGetIOBAR { device idx -- base ok }
    auto data
    device@ 0x10 idx@ 4 * + HALPCIReadl data!

    if (data@ -1 ==)
        STATUS_NOT_FOUND ok!
    end elseif (data@ 1 &)
        data@ 0xfffffffc & base!
        0 ok!
    end else
        STATUS_INVALID_OBJECT_TYPE ok!
    end
end

fn HALPCIGetMemoryBAR { device idx -- phyaddr size ok }
    auto data
    auto data2
    device@ 0x10 idx@ 4 * + HALPCIReadl data!

    if (data@ -1 ==)
        STATUS_NOT_FOUND ok!
        return
    end elseif (data@ 1 &)
        STATUS_INVALID_OBJECT_TYPE ok!
        return
    end

    if (data@ 1 >> 3 & 2 ==)
        device@ 0x10 idx@ 1 + 4 * + HALPCIReadl data2!

        if (data2@)
            STATUS_BAD_ADDRESS ok!
            return
        end
    end

    auto rs
    HALCPUInterruptDisable rs!

    auto cmd
    device@ PCICOMMAND HALPCIReadi cmd!
    device@ PCICOMMAND cmd@ PCICOMMANDIO PCICOMMANDMEM | ~ & HALPCIWritei

    device@ 0x10 idx@ 4 * + -1 HALPCIWritel
    device@ 0x10 idx@ 4 * + HALPCIReadl data2!
    device@ 0x10 idx@ 4 * + data@ HALPCIWritel

    device@ PCICOMMAND cmd@ HALPCIWritei

    rs@ HALCPUInterruptRestore

    data@ 0xfffffff0 & phyaddr!
    data2@ ~ 1 + size!
    0 ok!
end

fn HALPCIReadb { device offset -- value }
    device@ HALPCIDevice_BusB + gb device@ HALPCIDevice_SlotB + gb device@ HALPCIDevice_FunctionB + gb offset@
    HALPCIReadRawb value!
end

fn HALPCIReadi { device offset -- value }
    device@ HALPCIDevice_BusB + gb device@ HALPCIDevice_SlotB + gb device@ HALPCIDevice_FunctionB + gb offset@
    HALPCIReadRawi value!
end

fn HALPCIReadl { device offset -- value }
    device@ HALPCIDevice_BusB + gb device@ HALPCIDevice_SlotB + gb device@ HALPCIDevice_FunctionB + gb offset@
    HALPCIReadRawl value!
end

fn HALPCIWriteb { device offset value -- }
    device@ HALPCIDevice_BusB + gb device@ HALPCIDevice_SlotB + gb device@ HALPCIDevice_FunctionB + gb offset@
    value@ HALPCIWriteRawb
end

fn HALPCIWritei { device offset value -- }
    device@ HALPCIDevice_BusB + gb device@ HALPCIDevice_SlotB + gb device@ HALPCIDevice_FunctionB + gb offset@
    value@ HALPCIWriteRawi
end

fn HALPCIWritel { device offset value -- }
    device@ HALPCIDevice_BusB + gb device@ HALPCIDevice_SlotB + gb device@ HALPCIDevice_FunctionB + gb offset@
    value@ HALPCIWriteRawl
end
