//
// Implements the PC serial port driver.
//

#include "<df>/dragonfruit.h"

#include "<ll>/OSDLL/OS.h"
#include "<ll>/OSDLL/OSConsoleControl.h"

#include "<inc>/HALLog.h"
#include "<inc>/HALMap.h"
#include "<inc>/HALDriver.h"
#include "<inc>/HALRTC.h"
#include "<inc>/HALPCI.h"
#include "<inc>/HALInterrupt.h"
#include "<inc>/HALCPU.h"

#include "<inc>/Kernel.h"

#include "<inc>/Executive.h"

#include "<inc>/Memory.h"

#include "<inc>/Security.h"

#include "<inc>/IO.h"

#include "<inc>/Console.h"

const COM_BASE 0x3f8
const COM_IRQ 4

const RBR 0
const THR 0
const IER 1
const IIR 2
const LCR 3
const MCR 4
const LSR 5
const MSR 6

const DLL 0
const DLM 1

const IER_RDA  0x01
const IER_THRE 0x02

const IIR_NO_PENDING 0x01

const LCR_WORD_8 0x03
const LCR_DLAB   0x80

const LSR_DR   0x01
const LSR_THRE 0x20

const MCR_DTR  0x01
const MCR_RTS  0x02
const MCR_OUT1 0x04
const MCR_OUT2 0x08

const CLOCK 115200
const BAUD_RATE 115200

const DIVISOR (CLOCK BAUD_RATE 2 / + BAUD_RATE /)

externptr HALPCSerialInited

struct SerialPort
	4 Base
	KeDPC_SIZEOF DPC
endstruct

var Port 0

fn (HALInterruptHandler) DriverSerialInterrupt { trapframe int -- }
    auto serialport
    Port@ CoDuplexGetContext serialport!

    auto iir
    serialport@ SerialPort_Base + @ IIR + HALPCIInb iir!

    if (iir@ IIR_NO_PENDING &)
        return
    end

	auto dpc
	serialport@ SerialPort_DPC + dpc!

	Port@ // context1
	0 // context2
	DPCHIGHIMPORTANCE // importance
	dpc@ // dpc
	KeDPCEnqueue drop
end

fn (CoDuplexTXFunction) DriverSerialTX { c duplex -- ok }
	auto serialport
	duplex@ CoDuplexGetContext serialport!

    auto rs
    HALCPUInterruptDisable rs!

	if (serialport@ SerialPort_Base + @ LSR + HALPCIInb LSR_THRE & ~~)
        rs@ HALCPUInterruptRestore
		-1 ok!
		return
	end

	c@ serialport@ SerialPort_Base + @ THR + HALPCIOutb

    rs@ HALCPUInterruptRestore
    0 ok!
end

fn (CoDuplexFlushFunction) DriverSerialFlush { duplex -- }
	auto serialport
	duplex@ CoDuplexGetContext serialport!

	auto c

	while (1)
        auto rs
        HALCPUInterruptDisable rs!

		if (serialport@ SerialPort_Base + @ LSR + HALPCIInb LSR_THRE & ~~)
            rs@ HALCPUInterruptRestore
			break
		end

		auto ok
		duplex@ CoDuplexReadTransmitCharacter ok! c!

		if (ok@)
            rs@ HALCPUInterruptRestore
			break
		end

		c@ serialport@ SerialPort_Base + @ THR + HALPCIOutb

        rs@ HALCPUInterruptRestore
	end
end

fn (KeDPCFunction) DriverSerialDPCFunction { context1 context2 dpc -- }
	auto serialport
	context1@ CoDuplexGetContext serialport!

	while (1)
        auto rs
        HALCPUInterruptDisable rs!

		if (serialport@ SerialPort_Base + @ LSR + HALPCIInb LSR_DR & ~~)
            rs@ HALCPUInterruptRestore
			break
		end

		auto c
		serialport@ SerialPort_Base + @ RBR + HALPCIInb c!

        rs@ HALCPUInterruptRestore

		if (c@ 0x7f ==)
			'\b' c!
		end

		c@ // c
		context1@ // duplex
		CoDuplexInputCharacter drop
	end

	context1@ DriverSerialFlush
end

fn SerialCreate { name base -- serialport deviceobject }
	SerialPort_SIZEOF // bytes
	'SrlP' // tag
	0 // flags
	MmAllocWithTag ok! serialport!

	if (ok@)
		ok@ name@ "SerialCreate: failed to create %s (%i)\n" KeCrash
	end

	auto ok

	serialport@ // context
	pointerof DriverSerialTX // txfunc
	pointerof DriverSerialFlush // flushfunc
	OSDUPLEXFLAG_CONSOLE // flags
	0 // pri
	0 // quotablock
	1 // permanent
	name@ // name
	ACCESS_OWNER_READ ACCESS_GROUP_READ | ACCESS_OWNER_WRITE | ACCESS_GROUP_WRITE | // permissions
	CoDuplexObjectCreate ok! drop deviceobject!

	if (ok@)
		ok@ name@ "SerialCreate: failed to create %s (%i)\n" KeCrash
	end

	base@ serialport@ SerialPort_Base + !

	pointerof DriverSerialDPCFunction // function
	serialport@ SerialPort_DPC + // dpc
	KeDPCInitialize

	deviceobject@ IODeviceDirectoryInsert ok!

	if (DEBUGCHECKS)
		if (ok@)
			ok@ name@ "SerialCreate: failed to insert %s (%i)\n" KeCrash
		end
	end
end

fn (FDriverInit) DriverInit { stage -- ok }
    if (stage@ STAGE_THREAD ==)
        auto rs
        HALCPUInterruptDisable rs!

        IER_RDA IER_THRE | COM_BASE IER + HALPCIOutb
        LCR_DLAB COM_BASE LCR + HALPCIOutb
        DIVISOR 0xff & COM_BASE DLL + HALPCIOutb
        DIVISOR 8 >> COM_BASE DLM + HALPCIOutb
        LCR_WORD_8 COM_BASE LCR + HALPCIOutb
        MCR_DTR MCR_RTS | MCR_OUT1 | MCR_OUT2 | COM_BASE MCR + HALPCIOutb

        1 HALPCSerialInited!

        rs@ HALCPUInterruptRestore

		"ttyS0" COM_BASE SerialCreate IODevice_Extension + @ Port!

        pointerof DriverSerialInterrupt COM_IRQ IPLSERIAL HALInterruptRegister
    end

    0 ok!
end
